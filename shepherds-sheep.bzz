#
# In the ARGoS window, you can move the camera with the following commands:
#
# - Translation: W A S D Q E keys, right-click + drag
# - Rotation: left-click + drag
#
# Selection: shift-click on the robot
# Remove selection: shift-click elsewhere
#
# Bug: selection is finnicky in the top-down view (Camera 1).
# Solution: switch to another camera and it'll work.
#

#
# Compiling and running the code
#
# - In the ARGoS window: Pause the simulation (if it's not paused already)
# - In the editor: do what you need, save, press the "Execute" button
# - In the ARGoS window: Press the "Play", "Step", or "Fast Forward" buttons
#

#
# This editor sucks.
#
# I know.
#
# But when it's open, it detects whether the open files have been modified elsewhere.
# So open this editor and then open the file you wanna edit in your favorite editor.
# When you save in your editor, the changes will be reflected here. Press Execute to compile and run.
# If you want syntax highlighting, JavaScript is pretty similar to Buzz.
#

# This function is executed once at init time
function init() {
    math.rng.setseed(id)

    shepherds = swarm.create(0)
    shepherds.select(id % 4 == 0)
    sheep = shepherds.others(1)

    sheep.exec(function(){
        set_leds(255, 255, 255)
    })
}

# This function is executed at each time step
# The main loop is controller by ARGoS
function step() {
    shepherds.exec(shepherdControl)
    sheep.exec(sheepControl)
}

function shepherdControl() {
    set_wheels(10.0, 10.0)
}

function sheepControl() {
    # clustering
    cluster()
    # obstacle avoidance
    avoidObstacles()
    # shepherd avoidance
    avoidShepherds()
}

function cluster(){
    var v = {
        .x = 0,
        .y = 0
    }

    neighbors.kin().foreach(function(robot_id, sheep){
        v.x = v.x + sheep.distance * math.cos(sheep.azimuth)
        v.y = v.y + sheep.distance * math.sin(sheep.azimuth)
    })

    norm = math.sqrt(v.x^2 + v.y^2)

    if(neighbors.count() > 0){
        v.x = v.x / neighbors.count()
        v.y = v.y / neighbors.count()
        norm = norm / neighbors.count()
    }

    if (norm > 20){
        goto(v.x, v.y)
        debug.rays.add(0, 140, 0, v.x/100, v.y/100, 1.0)
    }
    else{
        set_wheels(math.rng.uniform(0.0, 20.0), math.rng.uniform(-20.0, 0.0))
    }
}

function avoidObstacles(){
    var v = {
        .x = 0,
        .y = 0
    }

    i = 0
    while(i < 8){
        v.x = v.x + proximity[i].value * math.cos(proximity[i].angle)
        v.y = v.y + proximity[i].value * math.sin(proximity[i].angle)
        i = i + 1
    }

    # if norm of v is less than threshold continue straight, otherwise turn away
    norm = math.sqrt(v.x^2 + v.y^2)

    if(norm > 0.8 and (proximity[0].value > 0 or proximity[7].value > 0 or proximity[1].value > 0)){
        set_wheels(5.0, -5.0)
    }
}

function avoidShepherds(){
    var v = {
        .x = 0,
        .y = 0
    }

    neighbors.nonkin().foreach(function(robot_id, shepherd){
        shepX = shepherd.distance * math.cos(shepherd.azimuth)
        shepY = shepherd.distance * math.sin(shepherd.azimuth)

        norm = math.sqrt(shepX^2 + shepY^2)

        if(norm < 50){
            debug.print(norm)
            v.x = v.x + shepX
            v.y = v.y + shepY
        }
    })

    if(v.x != 0 and v.y != 0){
        goto(-v.x, -v.y)
    }
}

# This function is executed when you press the reset button
function reset() {
}

# This function is executed once when you close the simulator
function destroy() {
}
